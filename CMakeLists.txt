# 최소 CMake 버전 설정
cmake_minimum_required(VERSION 3.10)

# 프로젝트 이름 및 버전
project(V274X_DAQ VERSION 1.0 LANGUAGES CXX)

# C++17 표준 사용
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED True)

# 프로젝트 설치 경로 설정
set(CMAKE_INSTALL_PREFIX ${CMAKE_SOURCE_DIR})

# 옵션을 통해 번들 앱 여부를 설정
option(BUNDLE_APP "Build as a macOS bundle app" OFF)

# Qt6, yaml-cpp, ROOT 라이브러리 찾기
find_package(ryml REQUIRED)
find_package(c4core REQUIRED)
find_package(ROOT REQUIRED COMPONENTS Hist RIO RHttp)
find_package(Qt6 COMPONENTS Core Gui Widgets Charts REQUIRED)

# 라이브러리 디렉토리 추가
# (라이브러리 디렉토리를 프로젝트의 라이브러리 경로로 수정)
link_directories(/usr/local/lib /opt/homebrew/lib ${CMAKE_SOURCE_DIR}/lib)

# 외부 라이브러리 링크
# (여기에 실제 라이브러리 이름을 추가)
find_library(CAEN_FELib_PATH NAMES CAEN_FELib PATHS /usr/local/lib)

# v2740 관련 소스 파일 목록 설정
set(V2740_SOURCES
    CAENV2740.cxx
    CAENV2740Par.cxx
    CAENV2740Event.cxx
    QCAENV2740.cxx
    SharedMemory.cxx
    OnlineMonitor.cxx
    # 필요한 다른 소스 파일들을 여기에 추가하세요
)
#########################################################################
# v2740 공유 라이브러리 생성
add_library(v2740 SHARED ${V2740_SOURCES})
set_target_properties(v2740 PROPERTIES
   AUTOMOC ON
   AUTOUIC ON
   AUTORCC ON
)

# v2740 라이브러리에 대한 include 디렉토리 설정
target_include_directories(v2740 PUBLIC ${ROOT_INCLUDE_DIRS} /usr/local/include /opt/homebrew/include)

# v2740 라이브러리 링크
target_link_libraries(v2740 PRIVATE ${ROOT_LIBRARIES} ${CAEN_FELib_PATH} ryml c4core Qt6::Core Qt6::Gui Qt6::Widgets Qt6::Charts)

# v2740 라이브러리 설치
install(TARGETS v2740
    EXPORT v2740Targets
    LIBRARY DESTINATION lib
    ARCHIVE DESTINATION lib
    RUNTIME DESTINATION bin
)
#########################################################################

#########################################################################
# daq: console application
#########################################################################
# daq.cxx 컴파일 관련 내용 추가
add_executable(daq daq.cxx)
target_link_libraries(daq PRIVATE v2740)

# daq 실행 파일 설치
install(TARGETS daq
    RUNTIME DESTINATION bin
)
#########################################################################

#########################################################################
# v274x_daq: Qt application for data acquisition
#########################################################################
# v274x_daq.cxx 컴파일 관련 내용 추가
# The MACOSX_BUNDLE_ICON_FILE variable is added to the Info.plist
# generated by CMake. This variable contains the .icns file name,
# without the path.
set(MACOSX_BUNDLE_ICON_FILE macosx.icns)

# And the following tells CMake where to find and install the file itself.
set(app_icon_macos "${CMAKE_CURRENT_SOURCE_DIR}/icons/macosx.icns")
set_source_files_properties(${app_icon_macos} PROPERTIES
        MACOSX_PACKAGE_LOCATION "Resources")

# v274x_daq 실행 파일 추가
add_executable(v274x_daq v274x_daq.cxx)

if (BUNDLE_APP)
    set_target_properties(v274x_daq PROPERTIES
        MACOSX_BUNDLE TRUE
        MACOSX_BUNDLE_INFO_PLIST "${CMAKE_CURRENT_SOURCE_DIR}/Info.plist"  # Info.plist 경로 설정
        INSTALL_RPATH "${CMAKE_INSTALL_PREFIX}/lib"  # 설치 경로 설정
        BUILD_WITH_INSTALL_RPATH TRUE
        INSTALL_RPATH_USE_LINK_PATH TRUE
    )
    target_sources(v274x_daq PRIVATE ${app_icon_macos})  # 아이콘 파일 추가
endif()

# 라이브러리 링크 시 @rpath 사용
target_link_libraries(v274x_daq PRIVATE v2740 Qt6::Widgets ${ROOT_LIBRARIES})

# v274x_daq 실행 파일 설치
install(TARGETS v274x_daq
    RUNTIME DESTINATION bin
    BUNDLE DESTINATION bin  # BUNDLE DESTINATION 추가
)
#########################################################################

#########################################################################
# raw2root: console application for converting raw data to root data
#########################################################################
# raw2root 컴파일 관련 내용 추가
add_executable(raw2root raw2root.cxx)
target_include_directories(raw2root PRIVATE /usr/local/include ${ROOT_INCLUDE_DIRS})
target_link_libraries(raw2root PRIVATE v2740 ${ROOT_LIBRARIES})

# raw2root 실행 파일 설치
install(TARGETS raw2root
    RUNTIME DESTINATION bin
)
#########################################################################

#########################################################################
# sm_reader: console application for reading shared memory
#########################################################################
# sm_reader 컴파일 관련 내용 추가
add_executable(sm_reader sm_reader.cxx)
target_include_directories(sm_reader PRIVATE /usr/local/include ${ROOT_INCLUDE_DIRS})
target_link_libraries(sm_reader PRIVATE v2740 ${ROOT_LIBRARIES})

# sm_reader 실행 파일 설치
install(TARGETS sm_reader
    RUNTIME DESTINATION bin
)
#########################################################################

#########################################################################
# v274x_mon: Qt application for online monitoring
#########################################################################
# v274x_mon 컴파일 관련 내용 추가
add_executable(v274x_mon v274x_mon.cxx)
target_include_directories(v274x_mon PRIVATE /usr/local/include ${ROOT_INCLUDE_DIRS})
target_link_libraries(v274x_mon PRIVATE v2740 ${ROOT_LIBRARIES})

# v274x_mon 실행 파일 설치
install(TARGETS v274x_mon
    RUNTIME DESTINATION bin
)
#########################################################################

# 옵션으로 디버깅 정보 생성
if(CMAKE_BUILD_TYPE STREQUAL "Debug")
    add_definitions(-DDEBUG)
endif()

# 설치된 타겟을 사용할 수 있도록 설정
install(EXPORT v2740Targets
    FILE v2740Targets.cmake
    NAMESPACE v2740::
    DESTINATION lib/cmake/v2740
)